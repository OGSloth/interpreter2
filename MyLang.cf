-- programs ------------------------------------------------

entrypoints Program ;

Prog.  	   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

Argum. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Bloc.      Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

ConstDecl. Stmt ::= "const" Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

ArrayInit. Item ::= Ident "[" [Expr] "]" ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

ArrAss.    Stmt ::= Ident "[" [Expr] "]" "=" Expr ";" ;

TupAss.    Stmt ::= Ident "$" Expr "$" "=" Expr ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Ident "=" Expr "to" Expr ")" Stmt ;

Print.     Stmt ::= "print" "(" [Expr] ")" ";" ;

Break.     Stmt ::= "break" ;

Continue.  Stmt ::= "continue" ;

SExp.      Stmt ::= Expr  ";" ;

--Types ---------------------------------------------------

SimpleType.      Type ::= SType ;

CollectionType.  Type ::= CType ;

separator  Type "," ;

--Colection Types ------------------------------------------

Array.     CType ::= SType "[]" ;

Tuple.     CType ::= "$" [Type] "$" ;

--Simple Types ---------------------------------------------

Int.       SType ::= "int" ;

Str.       SType ::= "string" ;

Bool.      SType ::= "bool" ;

Void.      SType ::= "void" ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

EArr.      Expr6 ::= Ident "[" [Expr] "]" ;

ETup.      Expr6 ::= Ident "->" Expr ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

TR.        RelOp ::= "<>" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

